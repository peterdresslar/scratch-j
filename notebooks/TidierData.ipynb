{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "#### Peter Dresslar, OMT 548, Summer C 2025"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "This example is from a real-life chore I needed to complete recently. I had two spreadsheets, old and new, (converted to csv files) and needed to integrate the data from both into one sheet in tricky ways:\n",
    "\n",
    "- The old sheet has no ID row; the new one does---called \"ITEM NUMBER\"\n",
    "- There is no single key in the old sheet but we can set a compound key\n",
    "- The new sheet has additional (new rows)\n",
    "- The new sheet is missing some expense mappings from the old sheet that were manually-created and expensive to make. We need to preserve old sheet mappings. This is in column \"Budget Line Item.\"\n",
    "\n",
    "The following example is a \"pure Julia\" approach, although note that there is a bit of a pipeline. The comments are verbose.\n",
    "\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 56,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "old rows 8\n",
      "new rows 11\n",
      "compound keys in old sheet 8\n",
      "merged rows 11\n",
      "final row count: 11\n",
      "\u001b[1m11×8 DataFrame\u001b[0m\n",
      "\u001b[1m Row \u001b[0m│\u001b[1m item_number \u001b[0m\u001b[1m document \u001b[0m\u001b[1m budget_line_item \u001b[0m\u001b[1m document_date \u001b[0m\u001b[1m label           \u001b[0m\u001b[1m description            \u001b[0m\u001b[1m amount      \u001b[0m\u001b[1m account \u001b[0m\n",
      "     │\u001b[90m Int64       \u001b[0m\u001b[90m Int64    \u001b[0m\u001b[90m AbstractString   \u001b[0m\u001b[90m String15      \u001b[0m\u001b[90m String15?       \u001b[0m\u001b[90m String31               \u001b[0m\u001b[90m String15    \u001b[0m\u001b[90m Int64   \u001b[0m\n",
      "─────┼───────────────────────────────────────────────────────────────────────────────────────────────────────────────────────\n",
      "   1 │        1023         1  45                5/26/2023      PO20607          ABC LLC                 $9,900.00      -5505\n",
      "   2 │        1122         7  9                 5/30/2024      5/11/24          Retirement              -              -5123\n",
      "   3 │        1123         7  9                 5/30/2024      5/11/24          Workmen's Compensation  $100.93        -5124\n",
      "   4 │        1127         7  76                5/30/2024     \u001b[90m missing         \u001b[0m INDIRECT COST           $3,656.98      -5310\n",
      "   5 │        1192         7  61                5/30/2024      PO22586 (part.)  PDQ INC                 $146,111.09    -5308\n",
      "   6 │        1299        18  62                4/8/2025      \u001b[90m missing         \u001b[0m REFUND1                 -$99.00        -5308\n",
      "   7 │        1300        18  62                4/8/2025      \u001b[90m missing         \u001b[0m REFUND2                 -$15.00        -5308\n",
      "   8 │        1301        18  46                4/8/2025      \u001b[90m missing         \u001b[0m Refund for PO24311      -$2,750.42     -5308\n",
      "   9 │        1410        20  -                 5/22/2025     \u001b[90m missing         \u001b[0m PDQ INC                 $12,092.99     -5308\n",
      "  10 │        1411        20  -                 5/22/2025     \u001b[90m missing         \u001b[0m PDQ INC                 $12,118.03     -5308\n",
      "  11 │        1412        20  -                 5/22/2025     \u001b[90m missing         \u001b[0m Refund for PO20609      -$400.00       -5309\n"
     ]
    },
    {
     "data": {
      "text/plain": [
       "\"sorted_df1.csv\""
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "using CSV\n",
    "using DataFrames\n",
    "using Statistics\n",
    "\n",
    "# Read the CSV files directly into DataFrames\n",
    "old_df = DataFrame(CSV.File(\"1.csv\"))\n",
    "\n",
    "# drop the last column of old_df, it is empty\n",
    "old_df = old_df[:, 1:end-1]\n",
    "\n",
    "new_df = DataFrame(CSV.File(\"2.csv\"))\n",
    "\n",
    "# clean old_df column names to lowercase, underscores for spaces\n",
    "# while we are at it, make sure the budget_line_item is a string\n",
    "function cleancols!(df)\n",
    "    for col in names(df)\n",
    "       new_name = lowercase(replace(string(col), \" \" => \"_\"))\n",
    "       rename!(df, col => new_name)\n",
    "       if new_name == \"budget_line_item\"\n",
    "           df.budget_line_item = string.(df.budget_line_item)\n",
    "       end\n",
    "    end\n",
    "end\n",
    "\n",
    "cleancols!(old_df)  # not strictly necessary, but feels better\n",
    "cleancols!(new_df)\n",
    "\n",
    "# println(old_df)\n",
    "# println(new_df)\n",
    "\n",
    "println(\"old rows \", nrow(old_df))\n",
    "println(\"new rows \", nrow(new_df))\n",
    "\n",
    "\n",
    "# see https://dataframes.juliadata.org/stable/man/split_apply_combine/\n",
    "old_df_unique = combine(   # compound key using combine()\n",
    "    groupby(old_df, [\"document\", \"description\", \"amount\"], skipmissing=false),\n",
    "    \"budget_line_item\" => first => \"old_budget_line_item\" # pipeline to apply the old budget line items to the old budget rows\n",
    ")\n",
    "\n",
    "println(\"compound keys in old sheet \", nrow(old_df_unique))\n",
    "\n",
    "# println(old_df_unique)\n",
    "\n",
    "# Perform the merge: leftjoin to keep all new rows\n",
    "merged_df = leftjoin(new_df, old_df_unique, \n",
    "    on = [\"document\", \"description\", \"amount\"],\n",
    "    matchmissing = :equal)  # matchmissing = :equal or otherwise new rows are dropped.\n",
    "\n",
    "println(\"merged rows \", nrow(merged_df))\n",
    "\n",
    "# note that since we have new rows we will still have some empty \"-\" budget_line_items\n",
    "merged_df.budget_line_item = ifelse.(\n",
    "    # if the budget_line_item is missing or \"-\"\n",
    "    ismissing.(merged_df.budget_line_item) .| (merged_df.budget_line_item .== \"-\"),\n",
    "    # then use the old_budget_line_item\n",
    "    coalesce.(merged_df.old_budget_line_item, merged_df.budget_line_item),\n",
    "    # otherwise use the budget_line_item\n",
    "    merged_df.budget_line_item\n",
    ")\n",
    "\n",
    "# drop the temporary column\n",
    "select!(merged_df, Not(\"old_budget_line_item\"))\n",
    "\n",
    "# Sort by item_number\n",
    "sorted_df = sort(merged_df, \"item_number\")\n",
    "\n",
    "println(\"final row count: \", nrow(sorted_df))\n",
    "\n",
    "println(sorted_df)\n",
    "\n",
    "#output the sorted_df to a csv file\n",
    "CSV.write(\"sorted_df1.csv\", sorted_df)\n"
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Following is a version of the exact same chore using Tidier.jl:\n",
    "\n",
    "https://tidierorg.github.io/TidierData.jl/latest/\n",
    "\n",
    "From that website:\n",
    "\n",
    "> TidierData.jl is a 100% Julia implementation of the dplyr and tidyr R packages. Powered by the DataFrames.jl package and Julia’s extensive meta-programming capabilities, TidierData.jl is an R user’s love letter to data analysis in Julia.\n",
    "\n",
    "Let's check it out.\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 57,
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "old rows 8\n",
      "new rows 11\n",
      "compound keys in old sheet 8\n",
      "merged rows 11\n",
      "\u001b[1m11×8 DataFrame\u001b[0m\n",
      "\u001b[1m Row \u001b[0m│\u001b[1m item_number \u001b[0m\u001b[1m document \u001b[0m\u001b[1m budget_line_item \u001b[0m\u001b[1m document_date \u001b[0m\u001b[1m label           \u001b[0m\u001b[1m description            \u001b[0m\u001b[1m amount      \u001b[0m\u001b[1m account \u001b[0m\n",
      "     │\u001b[90m Int64       \u001b[0m\u001b[90m Int64    \u001b[0m\u001b[90m AbstractString   \u001b[0m\u001b[90m String15      \u001b[0m\u001b[90m String15?       \u001b[0m\u001b[90m String31               \u001b[0m\u001b[90m String15    \u001b[0m\u001b[90m Int64   \u001b[0m\n",
      "─────┼───────────────────────────────────────────────────────────────────────────────────────────────────────────────────────\n",
      "   1 │        1023         1  45                5/26/2023      PO20607          ABC LLC                 $9,900.00      -5505\n",
      "   2 │        1122         7  9                 5/30/2024      5/11/24          Retirement              -              -5123\n",
      "   3 │        1123         7  9                 5/30/2024      5/11/24          Workmen's Compensation  $100.93        -5124\n",
      "   4 │        1127         7  76                5/30/2024     \u001b[90m missing         \u001b[0m INDIRECT COST           $3,656.98      -5310\n",
      "   5 │        1192         7  61                5/30/2024      PO22586 (part.)  PDQ INC                 $146,111.09    -5308\n",
      "   6 │        1299        18  62                4/8/2025      \u001b[90m missing         \u001b[0m REFUND1                 -$99.00        -5308\n",
      "   7 │        1300        18  62                4/8/2025      \u001b[90m missing         \u001b[0m REFUND2                 -$15.00        -5308\n",
      "   8 │        1301        18  46                4/8/2025      \u001b[90m missing         \u001b[0m Refund for PO24311      -$2,750.42     -5308\n",
      "   9 │        1410        20  -                 5/22/2025     \u001b[90m missing         \u001b[0m PDQ INC                 $12,092.99     -5308\n",
      "  10 │        1411        20  -                 5/22/2025     \u001b[90m missing         \u001b[0m PDQ INC                 $12,118.03     -5308\n",
      "  11 │        1412        20  -                 5/22/2025     \u001b[90m missing         \u001b[0m Refund for PO20609      -$400.00       -5309\n"
     ]
    },
    {
     "data": {
      "text/plain": [
       "\"sorted_df_tidier.csv\""
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "using TidierData\n",
    "\n",
    "# import the data into the new dataframes, which are just jl.DataFrames\n",
    "old_df_tidier = DataFrame(CSV.File(\"1.csv\"))\n",
    "new_df_tidier = DataFrame(CSV.File(\"2.csv\"))\n",
    "\n",
    "# drop the last column of old_df_tidier, it is empty\n",
    "old_df_tidier = old_df_tidier[:, 1:end-1]  # there is not really a tidier shorthand\n",
    "\n",
    "function cleancols_tidier(df)\n",
    "    @chain df begin\n",
    "        @clean_names() # nice!!! defaults to lowercase and underscores\n",
    "        @mutate(budget_line_item = string(budget_line_item))\n",
    "    end\n",
    "end\n",
    "\n",
    "# clean the column names\n",
    "\n",
    "old_df_tidier = cleancols_tidier(old_df_tidier)\n",
    "new_df_tidier = cleancols_tidier(new_df_tidier)\n",
    "\n",
    "# println(old_df_tidier)\n",
    "# println(new_df_tidier)\n",
    "\n",
    "println(\"old rows \", nrow(old_df))\n",
    "println(\"new rows \", nrow(new_df))\n",
    "\n",
    "# get the compound keys and old budget line items\n",
    "old_df_tidier_unique = @chain old_df_tidier begin\n",
    "    @group_by(document, description, amount)\n",
    "    @summarize(old_budget_line_item = first(budget_line_item))\n",
    "    @ungroup()\n",
    "end\n",
    "\n",
    "println(\"compound keys in old sheet \", nrow(old_df_tidier_unique))\n",
    "\n",
    "# println(old_df_tidier_unique)\n",
    "\n",
    "# now we are ready to join using tidier\n",
    "# tidier has a natural join capability we can use for this\n",
    "merged_df_tidier = @left_join(new_df_tidier, old_df_tidier_unique)\n",
    "\n",
    "# println(merged_df_tidier)\n",
    "\n",
    "println(\"merged rows \", nrow(merged_df_tidier))\n",
    "\n",
    "# now we need to map the old_budget_line_item to the budget_line_item where needed\n",
    "# while we are at it we can finish up our tidy pipeline\n",
    "merged_df_tidier = @chain merged_df_tidier begin\n",
    "    @mutate(budget_line_item = ifelse.(\n",
    "        # if the budget_line_item is missing or \"-\"\n",
    "        ismissing.(budget_line_item) .| (budget_line_item .== \"-\"),\n",
    "        # then use the old_budget_line_item\n",
    "        coalesce.(old_budget_line_item, budget_line_item),\n",
    "        # otherwise use the budget_line_item\n",
    "        budget_line_item\n",
    "    ))\n",
    "    @select(Not(:old_budget_line_item)) # duump the column now that we have used it\n",
    "    @arrange(item_number) # sort by item_number\n",
    "end\n",
    "\n",
    "println(merged_df_tidier)\n",
    "\n",
    "# output the merged_df_tidier to a csv file\n",
    "CSV.write(\"sorted_df_tidier.csv\", merged_df_tidier)\n"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.11.5",
   "language": "julia",
   "name": "julia-1.11"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.11.5"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 2
}
